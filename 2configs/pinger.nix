{ pkgs, nixpkgs-unstable, ... }:
let
  port = 3030;
  domain = "pinger.rtinf.net";

  templates = {
    grafana = # yaml
      ''
        title: |
          {{- if eq .status "firing" }}
          üö® {{ .title | default "Alert firing" }}
          {{- else if eq .status "resolved" }}
          ‚úÖ {{ .title | default "Alert resolved" }}
          {{- else }}
          ‚ö†Ô∏è Unknown alert: {{ .title | default "Alert" }}
          {{- end }}
        message: |
          {{ .message | trunc 2000 }}
      '';
  };
  template-dir = pkgs.symlinkJoin {
    name = "ntfy-template-dir";
    paths =
      builtins.attrValues (builtins.mapAttrs (key: val: pkgs.writeTextDir "${key}.yml" val) templates)
      ++ [ ];
  };
in
{
  services.ntfy-sh = {
    enable = true;
    # TODO: remove when 2.14.* hits stable
    package = nixpkgs-unstable.ntfy-sh;
    settings = {
      listen-http = ":${toString port}";
      base-url = "https://${domain}";
      behind-proxy = true;
      auth-default-access = "deny-all";
      inherit template-dir;
    };
  };
  services.nginx = {
    enable = true;
    virtualHosts.${domain} = {
      enableACME = true;
      forceSSL = true;
      locations."/" = {
        proxyPass = "http://127.0.0.1:${toString port}";
        proxyWebsockets = true;
      };
    };
  };
}
